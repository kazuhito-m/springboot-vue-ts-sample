buildscript {
    ext {
        springBootVersion = '2.1.0.BUILD-SNAPSHOT'
        encoding = 'UTF-8'
        compatibility = 1.8
    }
    repositories {
        jcenter()
        maven { url "https://repo.spring.io/snapshot" }
        maven { url "https://repo.spring.io/milestone" }
        maven { url "https://plugins.gradle.org/m2/" }
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
        classpath("com.moowork.gradle:gradle-node-plugin:1.2.0")
    }
}

apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'
apply plugin: 'com.moowork.node'

group = 'com.github.kazuhito_m'
version = '0.0.1-SNAPSHOT'

sourceCompatibility = compatibility
targetCompatibility = compatibility

repositories {
    jcenter()
    maven { url "https://repo.spring.io/snapshot" }
    maven { url "https://repo.spring.io/milestone" }
}

ext['thymeleaf-layout-dialect.version'] = '2.3.0'
ext['thymeleaf.version'] = '3.0.9.RELEASE'

node {
    version = '10.5.0'
    download = true
    nodeModulesDir = file("${project.projectDir}/front")
}

// front(TypeScript)を予めビルドする依存設定。
processResources.dependsOn npm_run_build
npm_run_build.dependsOn npm_install

test.dependsOn npm_test

clean.dependsOn npm_run_clean
npm_run_clean.dependsOn npm_install


sourceSets {
    main {
        resources {
            srcDir file("front/dist")
        }
    }
}

// JavaクラスとSQLファイルの出力先ディレクトリを同じにし、コンパイルより前にSQLファイルを出力先ディレクトリにコピーする。
processResources.destinationDir = compileJava.destinationDir
compileJava.dependsOn processResources



dependencies {
    compile('org.springframework.boot:spring-boot-starter-web')
    compile("org.springframework.boot:spring-boot-starter-thymeleaf")
    compile("org.seasar.doma:doma:2.19.2")
    compile("org.seasar.doma.boot:doma-spring-boot-starter:1.1.1")
    compile("org.springframework.boot:spring-boot-starter-jdbc")

    runtime("com.h2database:h2:1.4.196")

    testCompile('org.springframework.boot:spring-boot-starter-test')
}
